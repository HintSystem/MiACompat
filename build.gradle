plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
    id "me.modmuss50.mod-publish-plugin" version "1.1.0"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

// Load local properties if file exists
def localPropertiesFile = rootProject.file('local.gradle.properties')
if (localPropertiesFile.exists()) {
    def localProperties = new Properties()
    localProperties.load(new FileInputStream(localPropertiesFile))
    localProperties.each { key, value ->
        project.ext[key as String] = value
    }
}

repositories {
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }

    maven { url "https://maven.terraformersmc.com/" } // Mod menu
    maven { url "https://maven.isxander.dev/releases" } // YACL

    mavenCentral()
}

configurations {
    xaeroLib
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"

    modImplementation "dev.isxander:yet-another-config-lib:3.8.2+1.21.11-fabric"
    modImplementation "com.terraformersmc:modmenu:17.0.0-beta.2"

    modImplementation 'maven.modrinth:ping-wheel:1.12.1-fabric,1.21.11'

    modImplementation 'maven.modrinth:xaeros-minimap:fabric-1.21.11-25.3.5'
    modImplementation 'maven.modrinth:xaeros-world-map:fabric-1.21.11-1.40.6'

    xaeroLib 'maven.modrinth:xaeros-minimap:fabric-1.21.11-25.3.5'
}

// Extract xaerolib jar-in-jar dependency
def xaeroLibDir = 'build/xaerolib'
copy {
    from(zipTree(configurations.xaeroLib.singleFile)) {
        include 'META-INF/jars/xaerolib-*.jar'
        eachFile { fcd ->
            fcd.path = fcd.name
        }
    }
    into xaeroLibDir
    includeEmptyDirs = false
}

dependencies {
    def libJar = fileTree(xaeroLibDir).matching { include '*.jar' }.singleFile
    modImplementation files(libJar)
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
	}
}

publishMods {
    file = remapJar.archiveFile
    additionalFiles.from(remapSourcesJar.archiveFile)

    changelog = file("CHANGELOG.md").text
    modLoaders.add("fabric")
    type = STABLE

    modrinth {
        accessToken = project.findProperty("MODRINTH_API_KEY") ?: providers.environmentVariable("MODRINTH_API_KEY")
        projectId = "Jmct2KwW"
        minecraftVersions.addAll("1.21.11")

        requires("fabric-api")
        requires("yacl")
        optional("xaeros-minimap")
        optional("xaeros-world-map")
        optional("ping-wheel")
    }
}